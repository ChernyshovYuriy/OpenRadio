apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    def versionPropsFile = file('version.properties')
    def code = 1
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        code = versionProps['VERSION_CODE'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties !")
    }
    println "Version code:" + code
    defaultConfig {
        applicationId "com.yuriy.openradio"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode code
        versionName "2.5"

        jackOptions {
            enabled true
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        /*
        applicationVariants.all { variant ->
            def f = variant.outputFile
            def fname = f.name.replace(".apk",
                    "-${defaultConfig.versionName}.apk")
            variant.outputFile = new File(f.parent, fname)
        }
        */
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    supportVersion = "26.0.0-beta2"
    //supportVersion = "25.4.0"
}

dependencies {
    compile 'log4j:log4j:1.2.17'
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
        exclude group: 'org.hamcrest'
    }
    androidTestCompile 'org.hamcrest:hamcrest-all:1.3'
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("assembleRelease")) {
        task.doLast() {
            tasks.getAndIncrementVersionCode.execute()
        }
    }
}

task getAndIncrementVersionCode << {

    println "Get And Increment Version code"

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        println "Version code incremented to " + code
    } else {
        throw new GradleException("Could not read version.properties !")
    }
}
